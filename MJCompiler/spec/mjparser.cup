

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected;
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

init with {:
	errorDetected = false;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, LBRACE, RBRACE, SEMI, LPAREN, RPAREN, EQUAL, RETURN, PLUS, COMMA, PRINT;
terminal Integer NUMBER;
terminal String IDENT;
terminal BREAK, CLASS, ENUM, ELSE, CONST, IF, DO, WHILE, NEW, READ, VOID, EXTENDS;
terminal CONTINUE, THIS, SUPER, GOTO;
terminal String CHAR, BOOL;
terminal MINUS, MUL, DIV, MOD, DOUBLEEQUAL, NOTEQUAL, BIGGER, BIGGEREQUAL, SMALLER, SMALLEREQUAL, ANDAND, OROR, INCREMENT, DECREMENT, COLON, DOT, LBRACKET, RBRACKET;
terminal TRIPLEDOT;

nonterminal VarDecl, Type;
nonterminal OptArray, VarElem, VarArray;
nonterminal DeclList, SingleDecl, ConstDecl;
nonterminal ConstArray;
nonterminal VarDeclList;
nonterminal MethodDeclList, MethodType;
nonterminal FormPars, FormParsElem, FormParsArray;
nonterminal StatementList, SingleStatement;
nonterminal SingleStatementElem, Label;
nonterminal DesignatorStatement, DsgArray;
nonterminal Assignop, OptActPars, ActPars;
nonterminal OptSub, Addop;
nonterminal Mulop, OptConst;
nonterminal FactorOptActPars;
nonterminal Relop;
nonterminal VarArgs, ParsArgsWrapper;
nonterminal OptArgs, OptArgsArray, OptArgsElem;
nonterminal DoWhileBegin, ArrayRelop, OptMinus, LabelStmt;
nonterminal IfLparen, IfRparen, Helper, ElseHelp, OrHelp;
nonterminal DoWhileRparen, DoWhileLparen;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl, MethodTypeName, Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Term, TermArray;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor, Expr, ExprOpt;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ConstType, ConstElem;
nonterminal rs.etf.pp1.symboltable.concepts.Struct FactorUn, OptExpr, DsgOpt, DsgElem;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition, CondTerm, CondFact;

precedence left ELSE;

Program 		::= (Program) PROG ProgName:p DeclList LBRACE MethodDeclList RBRACE ;

ProgName		::= (ProgName) IDENT:ProgName;

MethodDeclList	::= (ListMethodDecl) MethodDeclList MethodDecl
				|	(NoMethodDeclList) ;
				
MethodDecl		::= (MethodDecl) MethodTypeName LPAREN ParsArgsWrapper RPAREN VarDeclList LBRACE StatementList RBRACE;

MethodTypeName	::= (MethodTypeName) MethodType:retType IDENT:methName;

ParsArgsWrapper ::= (Wrapper1) FormPars
				|	(Wrapper2) FormParsElem FormParsArray COMMA OptArgsArray
				|	(Wrapper3) OptArgsArray;
				
OptArgs			::= (YesOptArgs) OptArgsArray
				|	(NoOptArgs) ;

OptArgsArray	::= (ArrayOfOptArgs) OptArgsElem COMMA OptArgsArray
				|	(NoOptArgsArray) OptArgsElem;
				
OptArgsElem		::= (OptArgsElem) Type IDENT:t EQUAL ConstType;

StatementList	::= (ListOfStatements) StatementList SingleStatement
				|	(NoStatementList) ;
				
SingleStatement	::=	(SingleStatementLabel) LabelStmt COLON SingleStatementElem
				|	(SingleStatementnoLabel) SingleStatementElem;
				
LabelStmt		::= (LabelStmt) Label;

SingleStatementElem		::=	(SingleStmtElemDsgStmt) DesignatorStatement SEMI
						|	(SingleStmtElemIfElse) IF IfLparen Condition IfRparen SingleStatement ElseHelp SingleStatement Helper
						|	(SingleStmtElemIf) IF IfLparen Condition IfRparen SingleStatement Helper
						|	(SingleStmtElemDo) DoWhileBegin SingleStatement WHILE DoWhileLparen Condition DoWhileRparen SEMI
						|	(SingleStmtElemBreak) BREAK SEMI
						|	(SingleStmtElemContinue) CONTINUE SEMI
						|	(SingleStmtElemReturn) RETURN ExprOpt:optRet SEMI
						|	(SingleStmtElemRead) READ LPAREN Designator RPAREN SEMI
						|	(SingleStmtElemPrint) PRINT LPAREN Expr OptConst RPAREN SEMI
						|	(SingleStmtElemGoto) GOTO Label SEMI
						|	(SingleStmtElemNewStmt) LBRACE StatementList RBRACE;
		
DoWhileBegin	::=	(DoWhileBegin) DO;

DoWhileRparen	::= (DoWhileRparen) RPAREN;

DoWhileLparen	::= (DoWhileLparen) LPAREN;

IfLparen	::= (IfLparen) LPAREN;

IfRparen	::= (IfRparen) RPAREN;

ElseHelp	::= (ElseHelp) ELSE;

Helper		::= (Helper);
				
Condition		::=	(MultipleCondition) Condition OrHelp CondTerm
				|	(SingleCondition) CondTerm;
				
CondTerm		::= (MultipleCondTermFacts) CondTerm ANDAND CondFact
				|	(SingleCondTermFact) CondFact;
				
CondFact		::= (DoubleCondFact) Expr:e1 Relop Expr:e2
				|	(SingleCondFact) Expr;
				
OrHelp			::= (OrHelp) OROR;

DesignatorStatement		::=	(DesignatorStatement) Designator:dest DsgArray;

DsgArray		::=	(DsgArrayAE) Assignop Expr
				|	(DsgArrayActPars) LPAREN OptActPars RPAREN
				|	(DsgArrayInc) INCREMENT
				|	(DsgArrayDec) DECREMENT;
				
ExprOpt			::=	(YesExprOpt) Expr:e
				|	(NoExprOpt) ;
				
OptConst		::= (YesOptConst) COMMA NUMBER
				|	(NoOptConst) ;
				
Expr			::= (Expr) TermArray:ta;

TermArray		::= (MultipleTerm) TermArray:te Addop Term:t
				|	(SingleTerm) Term:t;
				
Term			::= (MultipleFactor) Term:t Mulop FactorUn:f
				|	(SingleFactor) FactorUn:f;
				
FactorUn		::=	(FactorMinus) OptMinus Factor
				|	(FactorNoMinus) Factor;
				
OptMinus		::= (OptMinus) MINUS;
				
Factor			::=	(FactorDesg) Designator:d FactorOptActPars
				|	(FactorNum) NUMBER
				|	(FactorChar) CHAR
				|	(FactorBool) BOOL
				|	(FactorNew) NEW Type OptExpr
				|	(FactorExpr) LPAREN Expr RPAREN;
				
Designator		::= (Designator) IDENT DsgOpt;

DsgOpt			::= (DesignatorOpt) DsgOpt DsgElem
				|	(NoDsgOpt);
				
DsgElem			::= (DsgElemIdent) DOT IDENT
				|	(DsgElemExpr) LBRACKET Expr RBRACKET;
				
OptExpr			::=	(OptionalExpr) LBRACKET Expr RBRACKET
				|	(NoOptExpr);

FactorOptActPars	::=	(OptActParsFactor) LPAREN OptActPars RPAREN
					|	(NoFactorOptActPars) ;
				
Mulop			::= (MulopMul) MUL
				|	(MulopDiv) DIV
				|	(MulopMod) MOD;
				
Addop			::=	(AddopPlus) PLUS
				|	(AddopMinus) MINUS;

OptSub			::= (YesOptSub) MINUS
				|	(NoOptSub) ;
				
Assignop		::=	(Assignop) EQUAL; 
				
OptActPars		::= (ActParsArray) ActPars
				|	(NoActPars) ;
				
ActPars			::= (MoreActPars) ActPars COMMA Expr
				|	(SingleActPars) Expr;

FormPars		::= (ListFormPars) FormParsElem FormParsArray
				|	(NoFormPars) ;
				
FormParsArray	::= (ArrayOfFormPars) FormParsArray COMMA FormParsElem
				|	(NoFormParsArray) ;
				
FormParsElem	::= (FormParsElem) Type IDENT:elem OptArray;

MethodType		::= (NonVoidTypeMethod) Type
				|	(VoidTypeMethod) VOID;

DeclList		::= (DeclListRec) DeclList SingleDecl
				|	(NoDeclList) ;
				
SingleDecl		::= (SingleVar) VarDecl
				|	(SingleConst) ConstDecl;

VarDeclList		::= (ListVarDecl) VarDeclList VarDecl
				|	(NoVarDeclList) ;
				
ConstDecl		::= (ConstDecl) CONST Type ConstElem ConstArray SEMI;

ConstArray		::= (ArrayOfConst) ConstArray COMMA ConstElem
				|	(NoConstArray) ;
				
ConstElem		::= (ConstElem) IDENT:ident EQUAL ConstType;

ConstType		::= (NumConstType) NUMBER	
				|	(CharConstType) CHAR
				|	(BoolConstType) BOOL;

VarDecl			::= (VarDecl) Type VarElem VarArray SEMI;

VarArray		::= (VarArrayRec) VarArray COMMA VarElem
				|	(NoVarArray) ;

VarElem			::= (VarElem) IDENT:varElem OptArray;

Type			::= (Type) IDENT:typeName;

OptArray 		::= (Array) LBRACKET RBRACKET
				|	(NoArray) ;
				
Label			::=	(Label) IDENT;

Relop			::=	(ArrayRelop1) ArrayRelop 
				|	(RelopBigger) BIGGER
				|	(RelopBiggerEqual) BIGGEREQUAL
				|	(RelopSmaller) SMALLER
				|	(RelopSmallerEqual) SMALLEREQUAL;
				
ArrayRelop		::=	(RelopDoubleEqual) DOUBLEEQUAL
				|	(RelopNotEqual) NOTEQUAL;


